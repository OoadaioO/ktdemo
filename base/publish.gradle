apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


def String getLocalProperty(String key) {
    Properties properties = new Properties()
    rootProject.file("local.properties").withInputStream { properties.load(it) }
    return properties.getProperty(key)
}

ext {
    publish_group_id = "com.shuisechanggong"
    publish_artifactId = "base"


    publish_version = android.defaultConfig.versionName
    publish_package_desc = "base $publish_version"
    publish_vcsTag = "1.0.0"

    publish_repo = "com.shuisechanggong"
    publish_package = publish_artifactId
    publish_licenses = ['Apache-2.0']
    publish_vcs_url = "git@github.com:OoadaioO/ktdemo.git"
    publish_user = getLocalProperty("bintrayUser")
    publish_api_Key = getLocalProperty("bintrayApiKey")
    publish_description = "shuisechanggong base android lib"

}


version = publish_version
group = publish_group_id



publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            groupId publish_group_id
            artifactId publish_artifactId
            version publish_version


            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }

        }
    }
}


bintray {
    user = publish_user
    key = publish_api_Key
    configurations = ['archives']
    pkg {
        repo = publish_repo
        name = publish_package
        description = publish_description
        licenses = publish_licenses
        vcsUrl = publish_vcs_url

        version {
            name = publish_version
            desc = publish_package_desc
            released = new Date()
            vcsTag = publish_vcsTag
        }
    }

    publications = ['Production']
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}